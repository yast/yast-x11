/**************
FILE          : x11_proposal.ycp
***************
PROJECT       : YaST2
              :
AUTHOR        : Marcus Sch√§fer <ms@suse.de>
              :
BELONGS TO    : YaST2
              : (X11 integration part using SaX2/ISaX)
              :
DESCRIPTION   : Proposal function dispatcher for
              : X11 configuration
              :
              :
STATUS        : Development
**************/
/**
 * File:        proposal/x11_proposal.ycp
 * Package:     X11 Configuration
 * Summary:     Installation Proposal for X11 stuff
 * Authors:     Marcus Schaefer <ms@suse.de>
 *
 */
{
textdomain "x11";

import "Report";
import "Installation";
import "Arch";
import "Confirm";
import "XMessages";
import "XLib";
import "Package";
import "Popup";

include "x11/x11Dialog.ycp";

//===================================================
// Initialize proposal parameters
//---------------------------------------------------
string  func  = (string) WFM::Args(0);
map     param = (map)    WFM::Args(1);
map     ret   = $[];

//==========================================
// Check for graphics device // (bnc#431522)
//------------------------------------------
if ( (integer)SCR::Execute(.target.bash, "/sbin/lspci -n | cut -f 2 -d \" \" | grep -q \"0300\"") == 0 )
{
	y2milestone("Detected a graphics device: A PCI device exists with ClassID 0300.");
	y2milestone("X11 configuration will be performed.");
}
else
{
	y2milestone("No PCI device exists with ClassID 0300.");
	y2milestone("Now checking for a framebuffer device.");

	// check if the framebuffer device exists AND if it has content (bnc#565877)
	// newer kernels create it even if no framebuffer is available
	if ( (integer)SCR::Execute(.target.bash, "[ -e /proc/fb ] && [ `wc -l /proc/fb | cut -d\" \" -f 1` -gt 0 ]") == 0 )
	{
		y2milestone("Found a framebuffer device. X11 configuration will be perfomed.");
	}
	else
	{
		y2error("Neither a PCI device with ClassID 0300 nor a framebuffer device was found.");
		y2error("X11 configuration will not be performed now.");

		// prevent xserver starts (eg. via vnc) on machines without a graphics device (bnc#466352)
		y2warning("No graphics device was found, thus setting DISPLAYMANAGER_STARTS_XSERVER to _no_ in /etc/sysconfig/displaymanager.");
		if ( SCR::Write(.sysconfig.displaymanager.DISPLAYMANAGER_STARTS_XSERVER, "no") )
		{
		     y2milestone("Successfully set DISPLAYMANAGER_STARTS_XSERVER to _no_.");
		}
		else
		{
		    y2error("Setting DISPLAYMANAGER_STARTS_XSERVER to _no_ failed. VNC logins to this machine may fail.");
		}
		return $[];
	}
}
//==========================================
// Check for installed packages...
//------------------------------------------
if (
	(Arch::x11_setup_needed ()) ||
	(Installation::x11_setup_needed ())
) {
	list<string> requiredPacs = [
		"xorg-x11", "xorg-x11-server","xorg-x11-server-glx","libusb",
		"sax2", "sax2-gui", "sax2-ident", "sax2-tools","sax2-libsax",
		"sax2-libsax-perl"
	];

	// remove unavailable packages
	requiredPacs = filter(string pac, requiredPacs, {return Pkg::PkgProperties(pac) != nil;} );

	y2milestone("required X11 packages: %1", requiredPacs);

	boolean package = Package::InstallAllMsg (
		requiredPacs, XMessages::pacsMissing
	);
	y2milestone ("x11 package status is: <%1>",package);
	if (! package) {
		return $[];
	}
}
//===================================================
// Handle Arch flag
//---------------------------------------------------
if (
	(! Arch::x11_setup_needed ()) ||
	(! Installation::x11_setup_needed ()) ||
	(! Package::Installed ("sax2-gui"))
) {
	if ( func != "Description" ) {
		ret = $[
			"rich_text_title"       : "",
			"menu_title"            : "",
			"id"                    : "",
			"preformatted_proposal" : "<b> </b>",
			"success"               : true
		];
		return ret;
	} else {
		return ret;
	}
}
//===================================================
// Create proposal for installation/configuration...
//---------------------------------------------------
if ( func == "MakeProposal" ) {
	//======================================
	// Initialize library cache
	//--------------------------------------
	if (! (boolean)XLib::isInitialized()) {
		y2milestone ("Loading library cache...");
		Popup::TimedMessage(XMessages::probeWarning, 15);
		XLib::loadApplication();
	}
	//======================================
	// Reading library cache data
	//--------------------------------------
	y2milestone ("Reading libsax cache data...");
	string	resolution  = (string)  XLib::getActiveResolution();
	string	colorDepth  = (string)  XLib::getActiveColorDepth();
	string	cardName    = (string)  XLib::getCardName();
	string	monitorName = (string)  XLib::getMonitorName();
	boolean	has3D       = (boolean) XLib::hasOpenGLFeatures();
	boolean isFbdevBased= (boolean) XLib::isFbdevBased();
	boolean	is3DCard    = (boolean) XLib::has3DCapabilities();
	boolean isNoteBook  = (boolean) XLib::isExternalVGANoteBook();
	boolean isNoteBookHW= (boolean) XLib::isNoteBookHardware();
	boolean extVGAactive= (boolean) XLib::isExternalVGAactive();
	list    monitorSize = (list)    XLib::getDisplaySize();
	string  monitorInch = (string)  monitorSize[0]:"undef";
//	integer tabletID    = (integer) XLib::getTabletID();     // (bnc#429770)
	integer tabletID    = 0;  // disabled due to (bnc#447622)

    // add more verbose logging
    y2milestone("Reading libsax data finished.");
    y2milestone("Creating proposal summary with the following data:");
    y2milestone("%1", $[
        "resolution"    : resolution,
        "colorDepth"    : colorDepth,
        "cardName"      : cardName,
        "monitorName"   : monitorName,
        "has3D"         : has3D,
        "isFbdevBased"  : isFbdevBased,
        "is3DCard"      : is3DCard,
        "isNoteBook"    : isNoteBook,
        "isNoteBookHW"  : isNoteBookHW,
        "extVGAactive"  : extVGAactive,
        "monitorSize"   : monitorSize,
        "monitorInch"   : monitorInch,
        "tabletID"      : tabletID
    ]);

	//======================================
	// Prepare proposal
	//--------------------------------------
	string UL = "<ul>";
	string LU = "</ul>";
	string LI = "<li>";
	string IL = "</li>";
	string proposal = "";
	string colorCount = "";
	if (colorDepth == "24") {
		colorCount = XMessages::colors16m;
	}
	if (colorDepth == "16") {
		colorCount = XMessages::colors65k;
	}
	if (colorDepth == "15") {
		colorCount = XMessages::colors32k;
	}
	if (colorDepth == "8") {
		colorCount = XMessages::colors256;
	}
	if (monitorName == "undef") {
		monitorName = XMessages::unconfigured;
	}
	//======================================
	// Create proposal
	//--------------------------------------
	proposal = UL;
	//======================================
	// Graphics card name...
	//--------------------------------------
	proposal = proposal
	+ LI
		+ XMessages::graphicsCard + cardName
	+ IL;
	//======================================
	// Monitor name and resolution...
	//--------------------------------------

    // generate a proper string for the display size
    string monitorSizeStr = XMessages::unconfigured;
    if (monitorInch != "undef")
    {
        monitorSizeStr =  monitorSize[0]:"" + " " + XMessages::Inches + " - " + XMessages::Aspect + " " + monitorSize[1]:"" + ":" + monitorSize[2]:"" ;
    }

    // open sublist for display settings
    if (!isFbdevBased) {
        proposal = proposal + LI + XMessages::displaySettings
         + UL + LI
                   + XMessages::monitor    + " " + monitorName    + " (<a href=\"sax-monitor\">" + _("change") + "</a>)"
              + IL + LI
                   + XMessages::Size       + " " + monitorSizeStr + " (<a href=\"sax-displaysize\">" + _("change") + "</a>)"
              + IL + LI
                   + XMessages::resolution + " " + resolution     + " (<a href=\"sax-resolution\">" + _("change") + "</a>)"
              + IL + LI
                   + XMessages::colorDepth + " " + colorDepth + " bit - " + colorCount +  " (<a href=\"sax-colors\">" + _("change") + "</a>)"
              + IL  ;
    } else {
        proposal = proposal + LI + XMessages::displaySettings
        + UL + LI
                   + XMessages::monitor    + " " + monitorName    + " </a>"
              + IL + LI
                   + XMessages::Size       + " " + monitorSizeStr + " </a>"
              + IL + LI
                   + XMessages::resolution + " " + resolution     + " </a>"
              + IL + LI
                   + XMessages::colorDepth + " " + colorDepth + " bit - " + colorCount + "</a>"
              + IL  ;
    }
    // sublist still open here - following items are within "display settings" section

    // 3D acceleration status/availability
    if (is3DCard)
    {
        string status = XMessages::deactivated;
        if (has3D)
        {
            status = XMessages::activated;
        }
        proposal = proposal + LI
                 + XMessages::acceleration +  " " + status  +  " (<a href=\"sax-3d\">" + _("change") + "</a>)"
                 + IL ;
    }

    // External VGA on NoteBooks
    if (isNoteBook)
    {
        string status = XMessages::deactivated;
        if (extVGAactive) { status = XMessages::activated; }

	if (!isFbdevBased) {
            proposal = proposal + LI
                     + XMessages::externalvga + " " + status + " (<a href=\"sax-ext-vga\">" + _("change") + "</a>)"
                     + IL;
        } else {
            proposal = proposal + LI
                     + XMessages::externalvga + " " + status + "</a>)"
                     + IL;
        }
    }
    else
    {
        if ((isNoteBookHW) && (! XMessages::popupDone))
        {
            XMessages::popupDone = true;
            y2milestone ("X11: DualHead Mode not supported with this NoteBook");
            //warnNoteBookPopup();
        }
     }

    // close the display settings sublist
    proposal = proposal + LU + IL ;


    //======================================
    // Tablet settings (fate#302888)
    //--------------------------------------
    string tabletStatus = XMessages::unconfigured;
    if ( tabletID != 0 )
    {
        string  tabletName  = (string)  XLib::getTabletModel();
        string  tabletVendor= (string)  XLib::getTabletVendor();

        tabletStatus = sformat("%1 - %2", tabletVendor, tabletName);
        y2milestone("Found tablet device: %1 (%2)", tabletName, tabletVendor);

        // if no tablet was found no item in the proposal will be shown
        proposal = proposal
            + LI
                + XMessages::Tablet + " " + tabletStatus
                // + " (<a href=\"sax-tablet\">" + XMessages::configure + "</a>)"
            + IL;
    }
	//======================================
	// Test your configuration now
	//--------------------------------------
	// deactivated due to problems with multiple X-Server instances
	//if (! isFbdevBased) {
	//	proposal = proposal
	//	+ LI
	//		+ "<a href=\"sax-test\">"
	//		+ XMessages::testConfig + "</a>"
	//	+ IL;
	//}
	//======================================
	// End proposal...
	//--------------------------------------
	proposal = proposal + LU;
	ret = $[
		"preformatted_proposal" : proposal,
		"links" : [
			"sax-monitor",
			"sax-resolution",
			"sax-displaysize",
			"sax-colors",
			"sax-3d",
			"sax-ext-vga",
			"sax-test"
			//,"sax-tablet"
		]
	];
	return ret;
}
//===================================================
// Handle user requests...
//---------------------------------------------------
else if ( func == "AskUser" ) {
	any chosenID = param["chosen_id"]:nil;
	if (chosenID == "sax-monitor") {
		y2milestone("X11: setup monitor...");
		string vendor = XLib::getMonitorVendor();
		string model  = XLib::getMonitorModel();
		map<string,list> cdb = XLib::getMonitorCDB();
		list selectedMonitor = setupMonitor ( cdb,vendor,model );
		y2milestone("X11: setup monitor <%1>",selectedMonitor);
		XLib::setMonitorCDB ( (list<string>)selectedMonitor );
	}
	if (chosenID == "sax-resolution") {
		y2milestone("X11: setup resolution...");
		string depth = XLib::getActiveColorDepth();
		string colstr = XMessages::color8bit;
		if (depth == "15") {
			colstr = XMessages::color15bit;
		}
		if (depth == "16") {
			colstr = XMessages::color16bit;
		}
		if (depth == "24") {
			colstr = XMessages::color24bit;
		}
		string selectedRes = setupResolutions (
			XLib::getAvailableResolutionNames(),
			XLib::getActiveResolutionString(),
			colstr
		);
		y2milestone("X11: setup resolution <%1>",selectedRes);
		XLib::setResolution ( selectedRes );
	}
	if (chosenID == "sax-displaysize") {
		y2milestone("X11: setup displaysize...");
		list<string> currentSize = XLib::getDisplaySize();
        // in case of undefined value fallback to a default
        if (currentSize[0]:"undef" == "undef")
            currentSize[0] = "15.0";
		list<string> selectedSize = setupDisplaySize (
			currentSize[0]:"15.0",
			currentSize[1]:"4" + "/" + currentSize[2]:"3"
		);
		y2milestone("X11: setup displaysize <%1>",selectedSize);
		XLib::setDisplaySize ( selectedSize );
	}
	if (chosenID == "sax-colors") {
		y2milestone("X11: setup color depth...");
		string depth = XLib::getActiveColorDepth();
		string selectedColor = setupColors (
			XLib::getActiveResolutionString(),
			depth
		);
		y2milestone("X11: setup color depth <%1>",selectedColor);
		XLib::setDefaultColorDepth ( selectedColor );
	}
	if (chosenID == "sax-3d") {
		y2milestone("X11: setup 3D...");
		boolean has3D = XLib::hasOpenGLFeatures();
		y2milestone("X11: setup 3D <%1>",has3D);
		if (! has3D) {
			XLib::activate3D();
		} else {
			XLib::deactivate3D();
		}
	}
	if (chosenID == "sax-ext-vga") {
		y2milestone("X11: setup external VGA...");
		boolean extVGAactive = XLib::isExternalVGAactive();
		y2milestone("X11: setup external VGA <%1>",extVGAactive);
		if (! extVGAactive) {
			XLib::activateExternalVGA();
		} else {
			XLib::deactivateExternalVGA();
		}
	}
	if (chosenID == "sax-test") {
		y2milestone("X11: running a test server now...");
		boolean testStatus = XLib::testConfiguration();
		y2milestone("X11: test run returns <%1>",testStatus);
		y2milestone("X11: refer to /var/log/Xorg.99.log for details");
	}
	if (chosenID == "x11_conf") {
		infoPopup();
	}
//	if (chosenID == "sax-tablet" ) {
//		y2milestone("X11: setup tablet...");
//		string vendor = XLib::getTabletVendor();
//		string model  = XLib::getTabletModel();
//		map<string,list> tcdb = XLib::getTabletCDB();
//		list selectedTablet = setupTablet(tcdb, vendor, model);
//		y2milestone("X11: setup tablet <%1>", selectedTablet);
//		XLib::setTablet( (list<string>)selectedTablet );
//	}

}
//===================================================
// Handle proposal description...
//---------------------------------------------------
else if ( func == "Description" ) {
	ret = $[
		"rich_text_title" : XMessages::proposalTitle,
		"menu_title"      : XMessages::proposalMenuTitle,
		"id"              : "x11_conf"
	];
}
//===================================================
// writing configuration
//---------------------------------------------------
else if ( func == "Write" ) {
	if (XLib::isFbdevBased()) {
		integer mode = XLib::getKernelFrameBufferMode();
		if (mode > 0) {
			y2milestone("X11: Updating bootloader to vga=0x%1",mode);
			XLib::setKernelFrameBufferMode (mode);
		}
	}

	boolean success = XLib::writeConfiguration();
	ret = $[ "success" : success ];
}
return ret;
}
